struct Bid {
  blindedBid: ByteVec,
  deposit: U256
}

Contract Auction(
  beneficiary: Address,
  biddingEnd: U256,
  revealEnd: U256,
  mut ended: Bool,
  mut highestBidder: Address,
  mut highestBid: U256
) {
  mapping[ByteVec, Bid] bids
  mapping[Address, U256] bidNum

  event AuctionEnded(winner: Address, amount: U256)

  enum ErrorCodes {
    InvalidArg = 0
    BiddingAlreadyEnded = 1
    InvalidBidderAddress = 2
    BiddingNotEnd = 3
    RevealAlreadyEnded = 4
    RevealNotEnd = 5
    AuctionEndAlreadyCalled = 6
  }

  @using(preapprovedAssets = true, assetsInContract = true, checkExternalCaller = false, updateFields = true)
  pub fn bid(bidder: Address, blindedBid: ByteVec, deposit: U256) -> () {
    assert!(size!(blindedBid) == 32, ErrorCodes.InvalidArg)
    assert!(blockTimeStamp!() <= biddingEnd, ErrorCodes.BiddingAlreadyEnded)
    assert!(isAssetAddress!(bidder), ErrorCodes.InvalidBidderAddress)

    let mut count = 0
    if (!bidNum.contains!(bidder)) {
      bidNum.insert!(bidder, 0)
    } else {
      count = bidNum[bidder]
    }

    let bidKey = toByteVec!(bidder) ++ toByteVec!(count)
    let newBid = Bid { blindedBid: blindedBid, deposit: deposit }
    bids.insert!(bidKey, newBid)
    bidNum[bidder] = count + 1
    transferTokenToSelf!(bidder, ALPH, deposit)
  }

  // Note that if there are too many bids, there may be insufficient gas when the reveal function is executed.
  // If you want to use this code in production, please consider supporting batch reveal bids.
  @using(assetsInContract = true, updateFields = true, checkExternalCaller = false)
  pub fn reveal(bidder: Address, values: ByteVec, fakes: ByteVec, secrets: ByteVec) -> () {
    assert!(blockTimeStamp!() > biddingEnd, ErrorCodes.BiddingNotEnd)
    assert!(blockTimeStamp!() <= revealEnd, ErrorCodes.RevealAlreadyEnded)

    let length = bidNum[bidder]
    assert!(size!(values) / 32 == length, ErrorCodes.InvalidArg)
    assert!(size!(fakes) == length, ErrorCodes.InvalidArg)
    assert!(size!(secrets) / 32 == length, ErrorCodes.InvalidArg)

    let mut refund = 0
    let mut remains = length
    for (let mut index = 0; index < length; index = index + 1) {
      let bidKey = toByteVec!(bidder) ++ toByteVec!(index)
      if (bids.contains!(bidKey)) {
        let value = u256From32Byte!(byteVecSlice!(values, index * 32, (index + 1) * 32))
        let fakeByte = byteVecSlice!(fakes, index, index + 1)
        let fake = if (fakeByte == #00) false else true
        let secret = byteVecSlice!(secrets, index * 32, (index + 1) * 32)

        let bidToCheck = bids[bidKey]
        if (bidToCheck.blindedBid == blake2b!(u256To32Byte!(value) ++ toByteVec!(fake) ++ secret)) {
          refund = refund + bidToCheck.deposit

          if (!fake && bidToCheck.deposit >= value && value > highestBid) {
            if (highestBidder != nullContractAddress!()) {
              // Refund the previously highest bidder.
              transferTokenFromSelf!(highestBidder, ALPH, highestBid)
            }

            highestBid = value
            highestBidder = bidder
            refund = refund - value
          }
          bids.remove!(bidKey)
          remains = remains - 1
        }

      } else {
        remains = remains - 1
      }
    }
    transferTokenFromSelf!(bidder, ALPH, refund)
    if (remains == 0) {
      bidNum.remove!(bidder)
    }
  }

  @using(assetsInContract = true, checkExternalCaller = false, updateFields = true)
  pub fn auctionEnd() -> () {
    assert!(blockTimeStamp!() > revealEnd, ErrorCodes.RevealNotEnd)
    assert!(!ended, ErrorCodes.AuctionEndAlreadyCalled)

    emit AuctionEnded(highestBidder, highestBid)
    ended = true
    transferTokenFromSelf!(beneficiary, ALPH, highestBid)
  }

  pub fn getBidNum(bidder: Address) -> U256 {
    return if (bidNum.contains!(bidder)) bidNum[bidder] else 0
  }

  pub fn getBid(bidder: Address, index: U256) -> Bid {
    let bidKey = toByteVec!(bidder) ++ toByteVec!(index)
    return bids[bidKey]
  }
}
